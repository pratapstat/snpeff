#!/usr/bin/env bds

#-------------------------------------------------------------------------------
#
# Create databases
#
#-------------------------------------------------------------------------------

#---
# Commands to run
#---
string snpeffDir="$HOME/snpEff"
string snpeffData="$snpeffDir/data"
string snpeff="java -Xmx10G -jar snpEff.jar "
string snpeffBuild="$snpeff build -v"

# Variables
string{} built

#-------------------------------------------------------------------------------
# Add a build command
#-------------------------------------------------------------------------------
bool addBuild(string type, string genome) {
	string dir = "$snpeffData/$genome"
	string db = "$dir/snpEffectPredictor.bin"
	string[] geneFiles = dir.dir("genes.*") 

	# No 'genes' file? Error
	if( geneFiles.isEmpty() ) {
		warning("No genes file for $genome\n")
		return( false );
	}

	string geneFile = geneFiles.head()
	if( built.hasKey(genome) ) {
		print("INFO : Genome $genome already built\n")
	} else if( db <- geneFile ) {
		print("BUILD:\t$type\t$genome\n")
		task $snpeffBuild -$type $genome 2>&1 | tee build.$genome.out
	} else {
		print("OKi  :\t$type\t$genome\n")
	}

	# Mark as built
	built{genome} = genome
}

#-------------------------------------------------------------------------------
# Main
#-------------------------------------------------------------------------------

# Special cases
addBuild("refseq", "hg19")
addBuild("gff2", "amel2")

# Look into all directories
print("Available databases:\n")
string dbids = sys $snpeff databases | cut -f 1 | tail -n +3

print("Building:\n")
for(string genome : dbids.stdout().lines()  ) {

	# Get genome name and genes file
	genome = genome.trim().baseName()
	string dir = "$snpeffData/$genome"
	string[] geneFiles = dir.dir("genes.*")

	if( ! geneFiles.isEmpty() ) {
		# Find genes' file type
		string geneFile = geneFiles.head()
		geneFile = geneFile.baseName(".gz")

		# Get type from gene's file extention
		string type = geneFile.extName()

		# Convert type names
		if( type == "gtf" )			{ type = "gtf22" }
		else if( type == "gff" )	{ type = "gff3" }
		else if( type == "gb" )		{ type = "genbank" }

		# Build
		addBuild(type, genome)
	} else {
		warning("No genes file found for '$genome', dir '$dir'\n")
	}
}

wait
print("Done!\n")

# #---
# # Build genomes
# #---
# ./scripts/queue.pl 24 24 1 queue_build.txt
# 
# #---
# # Special builds: Human
# #---
# $snpeff buildNextProt -v GRCh37.$ENSEMBL_RELEASE db/nextProt/
# 
# #---
# # Create build reports
# #---
# echo "Creating build report : cds_check.txt"
# grep "CDS check" *.stdout | cut -f 3- -d : | grep -v "^$" | sort | tee cds_check.txt
# 
# echo "Creating build report : protein_check.txt"
# grep "Protein check" *.stdout | cut -f 3- -d : | grep -v "^$" | sort | tee protein_check.txt
# 
